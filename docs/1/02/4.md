프로그래밍 언어에서 어떤 작업을 여러 번 실행하려면 반복문을 사용합니다.

매우 많은 반복은 지연을 일으키지만, 웬만한 상황에서는 매우 빠른 시간 안에 끝납니다.

하지만, 마인크래프트에서 명령어를 반복적으로 실행하는 방법은 `1`회 실행 후, `0.05`초라는 시간이 소요됩니다.

- 반복형 명령 블록
- `tick.json`
- `enchantment`
- `advancement`

즉, 명령어 실행 이후에 다음 실행까지 `1`틱이 소요되는 것인데,

예를 들어, 프로그래밍 입문 단계에서 한 번씩은 출력하는 별 찍기를 해야한다고 가정합시다.

```json
tick.json
   {"replace": false, "values":["test:tick"]}

test:tick
   data modify storage star: list append value "*"
   tellraw @a {nbt: "list[]", storage: "star:", separator: ""}

>>> *
0.05초 후
>>> **
0.05초 후
>>> ***
0.05초 후
>>> ****
0.05초 후
>>> *****

...
```

   <video width="640" height="360" controls>
      <source src="assets/vid/02-4/refu.mp4" type="video/mp4">
   </video>

<br/>

이런식으로 별 하나가 추가될 때마다 `0.05`초가 소요됩니다.  
별 `n`개를 찍기 위해서는 `n * 0.05` 초가 걸리는 셈인데,

즉각적인 연산을 필요로 하는 곳에서는 `0.05`초의 딜레이가 굉장히 큰 문제점으로 작용합니다.

그런데, 함수를 사용하면 동일한 틱 내에서 `n`번, 심지어는 `n`만번도 실행할 수 있습니다.

명령어에는 반복문이 없으므로 재귀함수를 통해 이를 구현합니다.

```mcfunction
test:a
   say recursion
   function test:a

>>> recursion

...
```

<br/>

우선 위 명령어를 실행하기 전에 `/gamerule` 명령어로 명령어 실행에 제한을 걸어야 합니다.

기본값이 `65,536`이기에 `32,000`번 정도 `say`가 실행될 겁니다.

이 값을 `20`으로 변경합니다. (총 `10`번 실행)

```mcfunction
gamerule maxCommandChainLength 20
```

   <video width="640" height="360" controls>
      <source src="assets/vid/02-4/recursion.mp4" type="video/mp4">
   </video> 

<br/>

매번 재귀함수를 작동할 때마다 `gamerule`을 변경할 수는 없으니 제한(재귀 탈출 조건)을 만들어 보도록 하겠습니다.

일단, 위에서 변경했던 `maxCommandChainLength` 값을 다시 `65,536`으로 변경합니다.

```mcfunction
gamerule maxCommandChainLength 20
```

```mcfunction
test:a
   scoreboard players set #count test 10

   function test:b

test:b
   scoreboard players remove #count test 1

   say recursion

   execute if score #count test matches 1.. run function test:b

>>> recursion [1]
>>> ...
>>> recursion [10]
```

   <video width="640" height="360" controls>
      <source src="assets/vid/02-4/recursion_store.mp4" type="video/mp4">
   </video> 

<br/>

이제는 `y0.0`에서 `y1.0`까지 `dust` 입자를 표시해 보겠습니다.

```mcfunction
tick.json
   {"replace": false, "values":["test:a"]}

test:a
   scoreboard players set #count test 8

   function test:b

test:b
   scoreboard players remove #count test 1

   particle dust{scale: 0.5, color:[1,0,0]}

   execute if score #count test matches 1.. positioned ~ ~.125 ~ run function test:b
```

   <video width="640" height="360" controls>
      <source src="assets/vid/02-4/1_block_re.mp4" type="video/mp4">
   </video> 

<br/>

`8`을 `16`으로 변경하면 `y2.0`까지 입자가 표시됩니다.

   <video width="640" height="360" controls>
      <source src="assets/vid/02-4/2_block_re.mp4" type="video/mp4">
   </video> 

<br/>

다시 함수를 불러오는 곳에 조건을 삽입해 여러 동작을 수행할 수 있습니다.

이제 구를 만들어 보도록 하겠습니다.

```mcfunction
tick.json
   {"replace": false, "values":["test:a"]}

test:a
   scoreboard players set #count test 90

   function test:b

test:b
   scoreboard players remove #count test 1

   execute rotated 0 ~ run particle dust{scale: 0.5, color:[1,0,0]} ^ ^ ^1
   execute rotated 45 ~ run particle dust{scale: 0.5, color:[1,0,0]} ^ ^ ^1
   execute rotated 90 ~ run particle dust{scale: 0.5, color:[1,0,0]} ^ ^ ^1
   execute rotated 135 ~ run particle dust{scale: 0.5, color:[1,0,0]} ^ ^ ^1
   execute rotated 180 ~ run particle dust{scale: 0.5, color:[1,0,0]} ^ ^ ^1
   execute rotated -135 ~ run particle dust{scale: 0.5, color:[1,0,0]} ^ ^ ^1
   execute rotated -90 ~ run particle dust{scale: 0.5, color:[1,0,0]} ^ ^ ^1
   execute rotated -45 ~ run particle dust{scale: 0.5, color:[1,0,0]} ^ ^ ^1

   execute rotated ~ 0 run particle dust{scale: 0.5, color:[1,0,0]} ^ ^ ^1

   execute rotated ~ 45 run particle dust{scale: 0.5, color:[1,0,0]} ^ ^ ^1
   execute rotated ~ -45 run particle dust{scale: 0.5, color:[1,0,0]} ^ ^ ^1

   execute if score #count test matches 1.. rotated ~4 ~-4 run function test:b
```

   <video width="640" height="360" controls>
      <source src="assets/vid/02-4/sphere.mp4" type="video/mp4">
   </video> 