주어진 여러 값에 대해 최소 혹은 최대 값을 출력합니다.
- 값은 모두 `scoreboard`로 주어집니다.
- 값은 모두 엔티티만 가지고 있습니다.

```js
console.log(Math.min(-1, -5, -10))
>>> -10

console.log(Math.max(1, 5, 10))
>>> 10
```

이와 같은 기능을 구현하는 것을 목표로 합니다.

<br/>

# 정렬 기준과 엔티티
---
가장 간편하면서 널리 알려진 방법입니다.

- 비용: 엔티티(최대 n개) + 청크(최소 1개)

## 구현
원점을 사용하므로 `0.0, 0.0, 0.0` 좌표를 항상 로드합니다.  
스코어 보드를 사용하므로 필요한 점수판을 생성합니다.
```mcfunction
execute in minecraft:overworld run forceload add 0 0

scoreboard objectives add min_max dummy
```

스코어 보드로 입력 값을 받습니다.  
입력 받은 엔티티의 수 만큼 표지를 소환한 후, 표지에 엔티티의 점수를 복사합니다.
```mcfunction
execute as @e[tag=entities] run function <copy>

# copy.mcfunction
   scoreboard players operation #copy min_max = @s min_max
   execute positioned 8. 0 8. summon marker run function <marker>

# marker.mcfunction
   tag @s add sorting
   scoreboard players operation @s min_max = #copy min_max
```

엔티티의 y 좌표를 입력 값으로 설정합니다.  
단, 엔티티의 y 좌표는 `20,000,000`을 넘길 수 없기에 `0.0000000004656612873077392578125`를 곱해서 저장합니다.
```mcfunction
execute as @e[tag=entities] run function <copy>

# copy.mcfunction
   scoreboard players operation #copy min_max = @s min_max
   execute positioned 8. 0 8. summon marker run function <marker>

# marker.mcfunction
   tag @s add sorting
   execute store result entity @s Pos[1] double .0000000004656612873077392578125 run scoreboard players operation @s min_max = #copy min_max
```

`8.0 0.0 8.0` 위치를 기준으로 `sort=` 인수를 이용해 표지를 탐색합니다.
- 최대: `/execute positioned 8. 0 8. run scoreboard players operation #max min_max = @n[tag=sorting, type=marker] min_max`
- 최소: `/execute positioned 8. 0 8. run scoreboard players operation #min min_max = @e[limit=1, tag=sorting, sort=furthest, type=marker] min_max`

최대/최소 값과 같은 엔티티는 다음 방법으로 찾을 수 있습니다.
- 최대: `execute as @e[tag=entities] if score @s min_max = #max min_max run say 최대 @s`
- 최소: `execute as @e[tag=entities] if score @s min_max = #min min_max run say 최소 @s`

<br/>

# operation 사용
---
엔티티를 사용하지 않는 가벼운 방법입니다.  
`/scoreboard`의 `operation`을 사용합니다.

## 구현
스코어 보드를 사용하므로 필요한 점수판을 생성합니다.
```mcfunction
scoreboard objectives add min_max dummy
```

### 최대
가짜 플레이어의 점수를 초기화합니다.
- `/scoreboard players reset #max min_max`

다음 명령어를 실행합니다.
```mcfunction
scoreboard players operation #max min_max > @e[tag=entities] min_max
```

`#max` 변수에 `entities` 중에서 가장 큰 값이 저장됩니다.

### 최소
가짜 플레이어의 점수를 가장 큰 값으로 설정합니다.
- `/scoreboard players set #min min_max 2147483647`

다음 명령어를 실행합니다.
```mcfunction
scoreboard players operation #min min_max < @e[tag=entities] min_max
```

<br/>

최대/최소 값과 같은 엔티티는 다음 방법으로 찾을 수 있습니다.
- 최대: `execute as @e[tag=entities] if score @s min_max = #max min_max run say 최대 @s`
- 최소: `execute as @e[tag=entities] if score @s min_max = #min min_max run say 최소 @s`